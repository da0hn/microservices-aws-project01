plugins {
  id 'java'
  id 'org.springframework.boot' version '3.2.4'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'com.palantir.docker' version '0.35.0'
}

group = 'dev.da0hn'
version = '1.3.0'

java {
  sourceCompatibility = '17'
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'commons-io:commons-io:2.16.1'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.testcontainers:mariadb'
  testImplementation 'io.rest-assured:rest-assured:5.4.0'
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.register('unpack', Copy) {
  dependsOn bootJar
  from(zipTree(file(tasks.bootJar.outputs.files.singleFile)))
  into('build/dependency')
}

docker {
  name "da0hn/${project.name}"
  buildx true
  platform 'linux/amd64'
  copySpec.from(tasks.unpack.outputs).into('dependency')
  buildArgs(['DEPENDENCY': 'dependency'])
  dockerfile file('Dockerfile')
}

